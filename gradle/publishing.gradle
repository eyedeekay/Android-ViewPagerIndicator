/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// configuration of the Maven artifacts
apply plugin: 'maven-publish'

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.source
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
  from androidJavadocs.destinationDir
  archiveClassifier.set("javadoc")
}

task androidSourcesJar(type: Jar) {
  from android.sourceSets.main.java.source
  archiveClassifier.set("source")
}

// add javadoc/source jar tasks as artifacts
artifacts {
  archives androidSourcesJar, androidJavadocsJar
}

publishing {
  publications {
    dist(MavenPublication) {
      groupId project.group
      artifactId project.name
      version project.version
      artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"

      artifact androidJavadocsJar {
        archiveClassifier.set("javadoc")
      }
      artifact androidSourcesJar {
        archiveClassifier.set("source")
      }

      pom.withXml {
        def Node root = asNode()
        root.appendNode('name', 'Android-ViewPagerIndicator')
        root.appendNode('description', 'Android library for adding indicators to view pagers.')
        root.appendNode('url', 'https://github.com/InkApplications/Android-ViewPagerIndicator')

        def issues = root.appendNode( 'issueManagement' )
        issues.appendNode('system', 'github' )
        issues.appendNode('url', 'https://github.com/InkApplications/Android-ViewPagerIndicator/issues')

        def scm = root.appendNode( 'scm' )
        scm.appendNode( 'url', 'https://github.com/InkApplications/Android-ViewPagerIndicator')
        scm.appendNode( 'connection', 'scm:git:https://github.com/InkApplications/Android-ViewPagerIndicator.git')
        scm.appendNode( 'developerConnection', 'scm:git:git@github.com/InkApplications/Android-ViewPagerIndicator.git')

        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode( 'name', 'The Apache Software License, Version 2.0' )
        license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0.txt' )
        license.appendNode( 'distribution', 'repo' )

        def dev = root.appendNode('developers').appendNode('developer')
        dev.appendNode('id', 'jakewharton')
        dev.appendNode('name', 'Jake Wharton')
        dev.appendNode('organization', 'InkApplications' )
        dev.appendNode('organizationUrl', 'http://inkapplications.com/')
      }
    }
  }
}
